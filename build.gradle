buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
    dependencies {
      classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:6.1.0"
    }
}

plugins {
	id "org.jdrupes.mdoclet" version "1.0.10"
    id 'org.ajoberstar.grgit' version '4.1.1-rc.4'
    id 'org.ajoberstar.git-publish' version '3.0.0'
}

apply plugin: 'biz.aQute.bnd.workspace'
// Makes Eclipse-groovy handle *.gradle file properly
apply plugin:'groovy'    

ext {
	isCiBuild = System.getenv().get("CI") == 'true'
    isJitPackBuild = System.getenv().get("JITPACK") == 'true'
}

// Prepare github authentication for plugins
if (System.properties['org.ajoberstar.grgit.auth.username'] == null) {
	System.setProperty('org.ajoberstar.grgit.auth.username',
		System.getenv("GH_TOKEN") ?: project.properties['github.token'] ?: "nouser")
}

allprojects {
    repositories {
        jcenter()
        // My packages, until linked
        // maven { url 'https://dl.bintray.com/mnlipp/jgrapes' }
        
        mavenCentral()
        // Snapshots
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // Experimental, not sure if this is the way to go...
        maven { url 'https://jitpack.io' }
    }
	
	apply plugin:'java'
		
    group = 'org.jgrapes'
}

/* Configure the subprojects */
subprojects {
    apply from: "${project.rootDir}/gradle/subprojects.gradle"
}

configurations {
    javadocTaglets
}

dependencies {
    javadocTaglets "org.jdrupes.taglets:plantuml-taglet:1.0.+"
    // javadocTaglets "com.github.mnlipp:jdrupes-taglets:master-SNAPSHOT"
}

javadoc {
    // Does not work on JitPack, no /usr/bin/dot
    enabled = JavaVersion.current().isJava8() && !isJitPackBuild
	
	options.tagletPath = configurations.javadocTaglets.files as List
	options.taglets = ["org.jdrupes.taglets.plantUml.Taglet"]
	
	classpath = files(subprojects.collect {project ->
		project.sourceSets.main.compileClasspath})
	source subprojects.collect {project -> project.sourceSets.main.allJava }
	options.addStringOption("doctitle", "JGrapes OSGi Components")
	options.addStringOption("overview", "overview.md")
	inputs.file "overview.md"
	options.addBooleanOption("use", true)
	options.addBooleanOption("linksource", true)
	options.stylesheetFile(file("stylesheet.css"))
	options.addStringOption("bottom", file("misc/javadoc.bottom.txt").text)
	options.links "https://docs.oracle.com/javase/8/docs/api/"
	destinationDir = file("../jgrapes.gh-pages/javadoc-osgi")
}

gitPublish {
	repoUri = 'https://github.com/mnlipp/jgrapes.git'
	branch = 'gh-pages'
	contents {
		from(javadoc) {
			into 'javadoc-osgi'
		}
	}
	preserve { include '**/*' }
	commitMessage = "Updated."
}

// Until https://github.com/ajoberstar/gradle-git-publish/issues/41 is fixed
tasks.gitPublishCopy.dependsOn javadoc

task stage {
	// Build everything first
	subprojects.tasks.each { tc -> dependsOn tc.findByName("build") }
	
	if (!isCiBuild || JavaVersion.current().isJava8()) {
    	// Publish JavaDoc
    	dependsOn gitPublishPush
    }	
}

configure(stage) {
	description = 'To be executed by travis, build and update JavaDoc.'
	group = 'build'
}

apply plugin: 'eclipse'

tasks.eclipse.dependsOn(cleanEclipse)
