dependencies {
    compile localGroovy()
}

if (plugins.hasPlugin('biz.aQute.bnd')) {
    
    // artifact_version is defined in cnf/bnd.bnd
    if (bnd.artifact_version) {
        project.version = bnd.artifact_version
    }
}

task releaseTag {
    doLast {
        String name = project.name.startsWith(project.group + ".") ?
            project.name.substring(project.group.length() + 1) : project.name
        name = name.replace('.', '-')
        String tag = name + "-" + project.version
        grgit.tag.add(name: tag, message: "Released.")
        grgit.push(refsOrSpecs: [tag])
    }
}

task releaseToBintray {
    group = "publishing"
    
    dependsOn "releaseTag"
    dependsOn "clean"
    dependsOn "build"
    dependsOn "bintrayUpload"
}

if (project.name.startsWith("org.jgrapes.")) {
    apply plugin: 'signing'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    group = "org.jgrapes"
        
    apply from: "${project.rootDir}/gradle/publishing.gradle"
    
    tasks.compileJava {
        if (!JavaVersion.current().isJava8()) {
            options.compilerArgs.addAll(['--release', '8'])
        }
    }
}

apply from: "${project.rootDir}/gradle/eclipse.gradle"

afterEvaluate {
    if(project.name == "cnf") {
        tasks.matching { it.group == "upload" ||
            it.group == "publishing" }.all {
            enabled = false
        }
    }
}