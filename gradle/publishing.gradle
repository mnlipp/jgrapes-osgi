// Additional jars for publishing
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.ajoberstar.grgit'

// Additional jars for publishing

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from (project.rootDir) { include 'README.md' }
    archiveClassifier = "javadoc"
}

// MavenPublishing (new)

publishing {

    repositories {
        maven {
            name "snapshot"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username project.properties['sonatypeTokenKey'] ?: "nouser"
                password project.properties['sonatypeTokenSecret'] ?: "nopass"
            }
        }
        maven {
            name "release"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }

        maven {
            name "GitLab"
            url "https://gitlab.com/api/v4/projects/24557189/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = System.getenv("GITLAB_REPO_ACCESS") 
                    ?: project.properties['gitLabRepoAccess'] ?: "" 
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

// Additional configuration of publishing
apply plugin: ConfigurePublishing

configurePublishing {
    withPomXml {
        asNode().with {
            appendNode('url', 'http://mnlipp.github.io/jgrapes/')
            appendNode('scm').with {
                appendNode('url', 'scm:git@github.com:mnlipp/jgrapes-osgi.git')
                appendNode('connection', 'scm:git@github.com:mnlipp/jgrapes-osgi.git')
                appendNode('developerConnection', 'git@github.com:mnlipp/jgrapes-osgi.git')
            }
            appendNode('licenses').with {
                appendNode('license').with {
                    appendNode('name', 'AGPL 3.0')
                    appendNode('url', 'https://www.gnu.org/licenses/agpl-3.0.en.html')
                    appendNode('distribution', 'repo')
                }
            }
            appendNode('developers').with {
                appendNode('developer').with {
                    appendNode('id', 'mnlipp')
                    appendNode('name', 'Michael N. Lipp')
                }
            }
        }
    }
}

task releaseTag {
    dependsOn "cleanJar"
    doLast {
        String name = project.name.startsWith(project.group + ".") ?
            project.name.substring(project.group.length() + 1) : project.name
        name = name.replace('.', '-')
        String tag = name + "-" + project.version
        grgit.tag.add(name: tag, message: "Released.")
        grgit.push(refsOrSpecs: [tag])
    }
}

build.mustRunAfter "releaseTag"

task stageOnOssrh {
    group = "publishing"
    
    dependsOn "releaseTag"
    dependsOn "clean"
    dependsOn "build"
    dependsOn "publishMavenPublicationToReleaseRepository"
}

// Disable un-applicable publishing mode
afterEvaluate {
    if (bnd.artifact_version.contains("SNAPSHOT")) {
        tasks.matching { it.name == "publishMavenJavaPublicationToReleaseRepository" }.all {
            enabled = false
        }
    } else {
        tasks.matching { it.name == "publishMavenJavaPublicationToSnapshotRepository" }.all {
            enabled = false
        }
    }
}

afterEvaluate {
    if(project.name == "cnf") {
        tasks.matching { it.group == "upload" ||
            it.group == "publishing" }.all {
            enabled = false
        }
    }
}
